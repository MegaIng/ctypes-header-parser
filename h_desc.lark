start: definition+

definition: "typedef" named_type ";" -> typedef
          | type ";" -> type_decl
          | named_type ";" -> var_decl
          | named_type "(" _parameters ")" ";" -> func_decl


named_type: type NAME
          | type "(" "*" NAME ")" "(" _parameters ")" -> function_type

_parameters: (parameter ("," parameter)*)?
parameter: named_type
         | type

type: NAME -> type_reference
    | "unsigned" NAME -> unsigned_type
    | "struct" [NAME] "{" (named_type ";")+ "}" -> full_struct
    | "struct" NAME -> struct_reference
    | type "*" -> ptr_type

NAME: /[^\W\d]\w*/

%ignore /\s+/